buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

ext.junit_version = '4.12'

// In this section you declare the dependencies for your production and test code
dependencies {
    // Unit testing and test-mocking
    testCompile "junit:junit:$junit_version"
}

// Enforce these in-case someone builds with the wrong version of java.
sourceCompatibility = 1.8
targetCompatibility = 1.8

idea {
    project {
        jdkName = "$sourceCompatibility"
        languageLevel = "$sourceCompatibility"
    }
}

test {
    def stdout = new LinkedList<String>()
    beforeTest { TestDescriptor td ->
        stdout.clear()
    }
    onOutput { TestDescriptor td, TestOutputEvent toe ->
        stdout.addAll(toe.getMessage().split('(?m)$'))
    }
    afterTest { TestDescriptor td, TestResult tr ->
        if (tr.resultType == TestResult.ResultType.FAILURE && stdout.size() > 0) {
            println()
            println("${td.className}.${td.name} failed, output:")
            stdout.each { print(it) }
        }
    }
    testLogging {
        exceptionFormat = 'full'
    }
}
